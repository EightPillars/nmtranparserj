$SIZES LTH=60
$PROB LILLY CONFIDENTIAL
$DATA OHD_11OCT2007_PGMPSM400_GI_prot.csv
$INPUT ID PAT WEEK TRT COMB TIME DOSE SEX ORGN AGE WT HT BMI FFM
DVID DV MDV EVID PROT
$EST MAX=9990 SIG=3 PRINT=1 NOABORT
METHOD=COND INTERACTION SLOW
 MSFO=608_QHWK_GMPSMGI400_BND4BLK4_DPAGP_NM7_C_CMD1.MSF
$COV

$THETA
(0.01,0.277,1.5)  ; POP_BFUNZ
(-50,-29.9,-0.25)  ; POP_T2BF YEAR

(0.01,0.237,5) ; POP_IPOTZ
(-30,-4.05,-0.25)  ; POP_T2IP YEAR

0 FIX ; KGZBF 1/MMOL/L
0 FIX ; KGZIP 1/PMOL/L
0 FIX ; KAGBFZ 1/Y
0.0218 ; KAGIPZ 1/Y

;OFFSET EFFECTS ON BETA CELL FUNCTION
;PRIMARY TREATMENT
0 FIX ; OBFPLA
(-10,0.784,10)   ; OBFGLZ
(-10,0.656,10)  ; OBFMET
(-10,0.487,10)  ; OBFPIO
0 FIX ; OBFGLB
0 FIX ; OBFGLM
0 FIX ; OBFROS
;COMBINATION TREATMENT
(-10,-0.134,10)  ; OBFSFU
(-10,-0.0816,10)   ; OBFMT2
0 FIX ; OBFINS
;DELAY
(0.01,2.67,30)  ; TBFGLZ WEEK
(0.01,6.66,30)  ; TBFMET WEEK
(0.01,15.3,30)  ; TBFPIO WEEK

;OFFSET EFFECTS ON INSULIN POTENCY
;PRIMARY TREATMENT
0 FIX ; OIPPLA
(-10,0.589,10)  ; OIPGLZ
(-10,0.851,10)  ; OIPMET
(-10,0.722,10)  ; OIPPIO
0 FIX ; OIPGLB
0 FIX ; OIPGLM
0 FIX ; OIPROS
;COMBINATION TREATMENT
(-10,-0.0588,10) ; OIPSFU
(-10,-0.0419,10) ; OIPMT2
0 FIX ; OIPINS
;DELAY
(0.001,30.,50)   ; TIPGLZ WEEK
(0.01,16.,100) ; TIPMET WEEK
(0.01,8.77,30) ; TIPPIO WEEK

;SLOPE EFFECTS ON BETA CELL FUNCTION
;PRIMARY TREATMENT
0 FIX ; SBFPLA
(-10,6.52,10)  ; SBFGLZ
(-10,7.85,10)  ; SBFMET
(-10,1.49,10)  ; SBFPIO
0 FIX ; SBFGLB
0 FIX ; SBFGLM
0 FIX ; SBFROS
;COMBINATION TREATMENT
(-10,5.12,10)  ; SBFSFU
(-10,6.55,10)  ; SBFMT2
0 FIX ; SBFINS

;SLOPE EFFECTS ON INSULIN POTENCY
;PRIMARY TREATMENT
0 FIX ; SIPPLA
(-10,1.29,10) ; SIPGLZ
(-10,0.616,10) ; SIPMET
(-10,0.356,10) ; SIPPIO
0 FIX ; SIPGLB
0 FIX ; SIPGLM
0 FIX ; SIPROS
;COMBINATION TREATMENT
(-10,0.236,10) ; SIPSFU
(-10,-0.152,10) ; SIPMT2
0 FIX ; SIPINS

;RESIDUAL ERROR
(0,0.105,)   ; RUV_CVFPG
(0,0.256,)   ; RUV_CVFSI

$OMEGA BLOCK(4) 
0.586 ; PPV_BFUNZ
0.484 0.727 ; PPV_KBFUN
0. -0.286 0.273 ; PPV_OBF
0. 0. 0.00307 0.00122 FIX ; PPV_TEQOBF
$OMEGA BLOCK(4) 
0.391 ; PPV_IPOTZ
0.0983 0.631 ; PPV_KIPOT
-0.142 -0.0475 0.508 ; PPV_OIP
0.0507 0.0129 -0.4 0.544 FIX ; PPV_TEQOIP


$OMEGA BLOCK(2)
0.09 ; PPV_RUVFPG
0. 0.0522 FIX ; PPV_RUVFSI
$SIGMA 1. FIX ; EPS1

$OMEGA
; DRUG EFFECT SLOPE RANDOM EFFECTS ARE PROBABLY CONFOUNDED
; WITH NATURAL HISTORY SLOPE RANDOM EFFECTS
0 FIX ; PPV_SBF
0 FIX ; PPV_SIP

$PRED

IF (NEWIND.LE.0) THEN
   LN2=LOG(2)
   TINY=1D-10
   TLAST=0
   GLAST=0
   ILAST=0

;RGLU, RINS, T2GLU AND T2INS IN HEALTHY SUBJECTS FROM
;SILBER HE, JAUSLIN PM, FREY N, GIESCHKE R, SIMONSSON US AND KARLSSON MO. 
;AN INTEGRATED MODEL FOR GLUCOSE AND INSULIN REGULATION IN HEALTHY VOLUNTEERS
;AND TYPE 2 DIABETIC PATIENTS FOLLOWING INTRAVENOUS GLUCOSE PROVOCATIONS. 
;J CLIN PHARMACOL 2007;47:1159-71

;HOMEOSTATIC VALUES FOR GLU AND INS
;WHEN BETA FUNCTION AND INSULIN SENSITIVITY ARE NORMAL (B=1, S=1)
   GLUNRM=4.9 ; MMOL/L
   INSNRM=45 ;PMOL/L
   VGLU=17.9 ;L
   VINS=6.09 ;L
   CLG=0.0894 ; L/MIN
   CLGI=0.001382 ; L/MIN/(PMOL/L)
   CLINS=1.22 ; L/MIN
   IPRG=1.42 ; INSULIN FEEDBACK POWER
   GPRG=-2.79 ; GLUCOSE FEEDBACK POWER
   GPROD0=0.741 ; MMOL/MIN
   IPROD0=54.9 ; PMOL/MIN
;GLUCOSE HOMEOSTATIC MAGIC NUMBER 
   GLUSTD=GLUNRM-1 ; MMOL/L
   ;NOTE BF AND IP MUST BE EQUAL TO 1 FOR THESE 'NORMAL' VALUES
   ;GLUCOSE CLEARANCE FOR NORMAL SUBJECT AT INSNRM
   RGLU=GPROD0 ;*EXP(PPV_RGLU) ; MMOL/MIN  0.741
   CLGLU=RGLU/(GLUNRM*INSNRM) ; APPARENT GLUCOSE CLEARANCE
   ;INSULIN CLEARANCE FOR NORMAL SUBJECT
   RINS=IPROD0 ;*EXP(PPV_RINS) ; PMOL/MIN 54.9
   CLINS=RINS/INSNRM ; APPARENT INSULIN CLEARANCE   
ENDIF


;DRUG EFFECT
;TREATMENT CODES
;UNKTRT=-1 PLACEBO=0 GLICLAZIDE=1 GLIBENCLAMIDE=2 GLIMEPIRIDE=3
;METFORMIN=4 PIOGLITAZONE=5 ROSIGLITAZONE=6

IF (TRT.LE.0) THEN
   EOBFT=THETA(9)
   EOIPT=THETA(22)
   ESBFT=THETA(35)
   ESIPT=THETA(45)
   KEQOBF=0
   KEQOIP=0
ENDIF
IF (TRT.EQ.1) THEN
   EOBFT=THETA(10) ;*DOSE/(OBFC5GLZ+DOSE)
   EOIPT=THETA(23)
   ESBFT=THETA(36)
   ESIPT=THETA(46)
   KEQOBF=LN2/(THETA(19)*EXP(ETA(4)))
   KEQOIP=LN2/(THETA(32)*EXP(ETA(8)))
ENDIF
IF (TRT.EQ.4) THEN
   EOBFT=THETA(11); *DOSE/(OBFC5MET+DOSE)
   EOIPT=THETA(24)
   ESBFT=THETA(37)
   ESIPT=THETA(47)
   KEQOBF=LN2/(THETA(20)*EXP(ETA(4)))
   KEQOIP=LN2/(THETA(33)*EXP(ETA(8)))
ENDIF
IF (TRT.EQ.5) THEN
   EOBFT=THETA(12) ;*DOSE ;/(OBFC5PIO+DOSE)
   EOIPT=THETA(25)
   ESBFT=THETA(38)
   ESIPT=THETA(48)
   KEQOBF=LN2/(THETA(21)*EXP(ETA(4)))
   KEQOIP=LN2/(THETA(34)*EXP(ETA(8)))
ENDIF

;COMBINATION CODES
;SULFONYLUREA=7 METFORMIN2=8 INSULIN=9

IF (COMB.EQ.0) THEN
   EOBFC=THETA(9)
   EOIPC=THETA(22)
   ESBFC=THETA(35)
   ESIPC=THETA(45)
ENDIF
IF (COMB.EQ.7) THEN ; SULFONYLUREA
   EOBFC=THETA(16)
   EOIPC=THETA(29)
   ESBFC=THETA(42)
   ESIPC=THETA(52)
; NO INTERACTION
;   EOBFC=OBFGLZ
;   EOIPC=OIPGLZ
;   ESBFC=SBFGLZ
;   ESIPC=SIPGLZ
ENDIF
IF (COMB.EQ.8) THEN ; METFORMIN
   EOBFC=THETA(17)
   EOIPC=THETA(30)
   ESBFC=THETA(43)
   ESIPC=THETA(53)
; NO INTERACTION
;   EOBFC=OBFMET
;   EOIPC=OIPMET
;   ESBFC=SBFMET
;   ESIPC=SIPMET
ENDIF
;IF (COMB.EQ.9) THEN
;   EOBFC=OBFINS
;   EOIPC=OIPINS
;   ESBFC=SBFINS
;   ESIPC=SIPINS
;ENDIF

;SLOPE DRUG EFFECT (FRACTIONAL CHANGE IN SLOPE)
SBF=(ESBFT+ESBFC)+ETA(11)
SIP=(ESIPT+ESIPC)+ETA(12)

BFUNZ=THETA(1)*EXP(ETA(1))
IPOTZ=THETA(3)*EXP(ETA(5))

IF (TIME.EQ.0) THEN
   BF=BFUNZ
   IP=IPOTZ
;SOLVE FOR BASELINE GLUCOSE AND INSULIN
   AX=1
   BX=-GLUSTD
   CX=-CLINS*RGLU/(IP*BF*CLGLU*RINS)
   FPGZ=(-BX + SQRT(BX*BX - 4*AX*CX))/(2*AX)
   FSIZ=RINS*BF*(FPGZ-GLUSTD)/CLINS
ELSE
   FPGZ=FPGZ
   FSIZ=FSIZ
ENDIF

IF (AGE.GT.0) THEN
   FAGBFZ=EXP(THETA(7)*(AGE-60))
   FAGIPZ=EXP(THETA(8)*(AGE-60))
ELSE
   FAGBFZ=1
   FAGIPZ=1
ENDIF
T2BF=THETA(2)*EXP(THETA(5)*(FPGZ-GLUNRM))*FAGBFZ
T2IP=THETA(4)*EXP(THETA(6)*(FPGZ-GLUNRM))*FAGIPZ
KBF=LN2/(T2BF*52)*(1+ETA(2)) ; Y -> WK
KIP=LN2/(T2IP*52)*(1+ETA(6)) ; Y -> WK


   IF (NEWIND.LE.1) THEN
      BFT=BFUNZ
      IPT=IPOTZ
      CEBFT=0
      CEIPT=0
      TLAST=TIME
      TDLTA=0
      ZBFT=0
      ZIPT=0
      ZCEBF=0
      ZCEIP=0
   ELSE
      TDLTA=TIME-TLAST
      ;DISEASE PROGRESSION
      BFT=ZBFT*EXP(KBF*(1+SBF)*TDLTA)
      IPT=ZIPT*EXP(KIP*(1+SIP)*TDLTA)
      ;EFFECT COMPARTMENTS
      EXPBFT=EXP(-KEQOBF*TDLTA)
      CEBF1=ZCEBF*EXPBFT
      CEBF2=EOBFT*(1-EXPBFT)
      CEBFT=ZCEBF*EXPBFT+EOBFT*(1-EXPBFT)
      EXPIPT=EXP(-KEQOIP*TDLTA)
      CEIPT=ZCEIP*EXPIPT+EOIPT*(1-EXPIPT)
   ENDIF
   ;OFFSET DRUG EFFECT 
   OBF=(CEBFT+EOBFC)*(1+ETA(3)) 
   OIP=(CEIPT+EOIPC)*(1+ETA(7))
   BF=BFT*(1+OBF)
   IP=IPT*(1+OIP)

;SOLVE FOR GLUCOSE AND INSULIN
   AX=1
   BX=-GLUSTD
   CX=-CLINS*RGLU/(IP*BF*CLGLU*RINS)
   FPG=(-BX + SQRT(BX*BX - 4*AX*CX))/(2*AX)
   FSI=RINS*BF*(FPG-GLUSTD)/CLINS 
   
;CODES FOR OBSERVATION TYPES
;DVIDTRT=0 DVIDCOMB=1 DVIDFPG=2 DVIDFSI=3 DVIDHBA1C=4
;   FPGZ=FPG
;   FSIZ=FSI
;   FPG=10
;   FSI=70
   IF (DVID.LE.2) THEN
      SDFPG=THETA(55)*FPG*EXP(ETA(9))
      Y=FPG+SDFPG*ERR(1)
   ENDIF
   IF (DVID.EQ.3) THEN
      SDFSI=THETA(56)*FSI*EXP(ETA(10))
      Y=FSI+SDFSI*ERR(1)
   ENDIF

;SAVE FOR RECURSIVE SOLUTION
   TLAST=TIME
   ZBFT=BFT
   ZIPT=IPT
   ZCEBF=CEBFT
   ZCEIP=CEIPT
   
$TABLE PROT ID TIME TRT COMB
FSI FPG BF IP
BFUNZ KBF IPOTZ KIP 
CEBFT CEIPT OBF OIP SBF SIP
DVID Y
ONEHEADER NOPRINT FILE=608_qhwk_gmpsmgi400_bnd4blk4_dpagp_nm7_c_cmd1.fit
